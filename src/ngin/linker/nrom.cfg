memory
{
    INES_HEADER:
        start = 0,
        size  = 16,
        type  = ro,
        fill  = yes;

    ZEROPAGE:
        start = 0,
        size  = 256,
        type  = rw;

    RAM:
        # Skip zeropage and stack.
        # \todo Define a memory area for stack so that it can be reused for
        #       other purposes.
        start = $200,
        size  = 2*1024 - 2*256,
        type  = rw;

    PRG_ROM:
        start = $8000,
        size  = 32*1024,
        type  = ro,
        fill  = yes;

    CHR_ROM:
        start = 0,
        size  = 8*1024,
        type  = ro,
        fill  = yes;
}

segments
{
    INES_HEADER:
        load = INES_HEADER,
        type = ro;

    CODE:
        load = PRG_ROM,
        type = ro;

    RODATA:
        load     = PRG_ROM,
        type     = ro,
        optional = yes;

    ZEROPAGE:
        load     = ZEROPAGE,
        type     = zp,
        optional = yes;

    SHADOW_OAM:
        load     = RAM,
        type     = bss,
        optional = yes;

    BSS:
        load     = RAM,
        type     = bss,
        optional = yes;

    VECTORS:
        load  = PRG_ROM,
        type  = ro,
        start = $FFFA;

    CHR_ROM:
        load     = CHR_ROM,
        type     = ro,
        optional = yes;
}
