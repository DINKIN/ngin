symbols
{
    # How big of a stack is used for function stack frames. The rest of the
    # stack can be used for other purposes.
    __ngin_stackSize: type = weak, value = 16;
}

memory
{
    INES_HEADER:
        start   = 0,
        size    = 16,
        type    = ro,
        fillval = 0,
        fill    = yes;

    ZEROPAGE:
        start = 0,
        size  = 256,
        type  = rw;

    STACK:
        start = $100,
        size  = 256 - __ngin_stackSize,
        type  = rw;

    RAM:
        # Skip zeropage and stack.
        start = $200,
        size  = 2*1024 - 2*256,
        type  = rw;

    PRG_ROM:
        start = $8000,
        size  = 32*1024,
        type  = ro,
        fill  = yes;

    CHR_ROM:
        start = 0,
        size  = 8*1024,
        type  = ro,
        fill  = yes;

    BUILD_LOG:
        start = 0,
        size  = 64*1024,
        type  = ro,
        file  = "%O-build-log.txt";
}

segments
{
    INES_HEADER:
        load = INES_HEADER,
        type = ro;

    NGIN_CODE:
        load   = PRG_ROM,
        type   = ro,
        define = yes,
        # The way alignment is handled in cc65 is sort of dumb at the moment.
        align  = 32;

    NGIN_RODATA:
        load     = PRG_ROM,
        type     = ro,
        optional = yes;

    NGIN_ZEROPAGE:
        load     = ZEROPAGE,
        type     = zp,
        optional = yes;

    NGIN_STACK:
        load     = STACK,
        type     = bss,
        optional = yes;

    NGIN_SHADOW_OAM:
        load     = RAM,
        type     = bss,
        optional = yes;

    NGIN_BSS:
        load     = RAM,
        type     = bss,
        optional = yes;

    NGIN_BUILD_LOG_HEADER:
        load     = BUILD_LOG,
        type     = ro,
        optional = yes;

    NGIN_BUILD_LOG:
        load     = BUILD_LOG,
        type     = ro,
        optional = yes;

    CODE:
        load   = PRG_ROM,
        type   = ro,
        define = yes,
        align  = 32;

    RODATA:
        load     = PRG_ROM,
        type     = ro,
        optional = yes;

    ZEROPAGE:
        load     = ZEROPAGE,
        type     = zp,
        optional = yes;

    STACK:
        load     = STACK,
        type     = bss,
        optional = yes;

    BSS:
        load     = RAM,
        type     = bss,
        optional = yes;

    VECTORS:
        load  = PRG_ROM,
        type  = ro,
        start = $FFFA;

    CHR_ROM:
        load     = CHR_ROM,
        type     = ro,
        optional = yes;

    BUILD_LOG:
        load     = BUILD_LOG,
        type     = ro,
        optional = yes;
}
